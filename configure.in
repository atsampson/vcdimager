AC_REVISION([$Id$])dnl
AC_INIT(libvcd/vcd.h)
AM_INIT_AUTOMAKE(vcdimager, 0.5.9alpha2)
AM_MAINTAINER_MODE

AM_SANITY_CHECK
AM_ACLOCAL_INCLUDE(frontends/gnome/macros)

dnl conditionally built frontends

AC_ARG_WITH(gnome-frontend,
	[  --with-gnome-frontend   enable gnome frontend (not enabled by default)], 
	enable_gnome_fe="${withval}", enable_gnome_fe=no)

AC_ARG_WITH(cli-frontend,
	[  --without-cli-frontend  enable cli frontend (enabled by default)], 
	enable_cli_fe="${withval}", enable_cli_fe=yes)

dnl programs

AC_PROG_CC
AC_PROG_CPP
if test "x$enable_gnome_fe" = "xyes"; then
 AC_PROG_CXX
fi
AM_PROG_CC_STDC
AC_PROG_RANLIB

dnl libs

if test "x$enable_cli_fe" = "xyes"; then
 AC_CHECK_LIB(popt, poptGetContext, POPT_LIBS="-lpopt", enable_cli_fe=no)
fi

if test "x$enable_gnome_fe" = "xyes"; then
 gnome_avail=no
 GNOME_INIT_HOOK(gnome_avail=yes,,)
 enable_gnome_fe="${gnome_avail}" 
fi

if test "x$enable_gnome_fe" = "xyes"; then
 AM_PATH_LIBGLADE(, enable_gnome_fe=no, gnome)
fi

if test "x$enable_gnome_fe" = "xyes"; then
 AM_PATH_GNOMEMM(,, enable_gnome_fe=no)
fi

dnl headers

AC_DEFINE(_GNU_SOURCE)
AC_STDC_HEADERS
AC_HAVE_HEADERS(stdint.h)
AC_HAVE_HEADERS(stdbool.h)

if test "x$enable_cli_fe" = "xyes"; then
 AC_CHECK_HEADER(popt.h,, enable_cli_fe=no)
fi

GNOME_COMPILE_WARNINGS

if test "x$enable_gnome_fe" = "xyes"; then
 GNOME_X_CHECKS
 dnl Subst PACKAGE_PIXMAPS_DIR.
 PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
 AC_SUBST(PACKAGE_PIXMAPS_DIR)
fi

dnl compiler
AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE

dnl system
WARN_CFLAGS="-Wall -Wmissing-prototypes -Wunused -Wpointer-arith -Wwrite-strings"
LIBS="$LIBS -lm"
CFLAGS="$CFLAGS $WARN_CFLAGS"

AM_CONDITIONAL(BUILD_CLI_FE, test "x$enable_cli_fe" = "xyes")
AM_CONDITIONAL(BUILD_GNOME_FE, test "x$enable_gnome_fe" = "xyes")

AC_SUBST(POPT_LIBS)

LIBVCD_CFLAGS='-I$(top_srcdir)/libvcd/'
LIBVCD_LIBS='$(top_builddir)/libvcd/libvcd.a'
AC_SUBST(LIBVCD_CFLAGS)
AC_SUBST(LIBVCD_LIBS)

AC_OUTPUT([ \
	Makefile \
	libvcd/Makefile \
	frontends/Makefile \
	frontends/cli/Makefile \
	frontends/gnome/Makefile \
	frontends/gnome/macros/Makefile \
	frontends/gnome/src/Makefile \
	vcdimager.spec \
])

echo "
Setup:
  Install path:		${prefix}
  Build CLI FE:		$enable_cli_fe
  Build Gnome FE:	$enable_gnome_fe
"

dnl EOF
